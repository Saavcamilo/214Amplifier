* Design Problem, ee114/214A- 2018
* Team Member 1 Name: Camilo Saavedra
* Team Member 2 Name: Kelly Woo
* Please fill in the specification achieved by your circuit 
* before you submit the netlist.
**************************************************************
* sunetids of team members = 05963993, 05961405
* The specifications that this script achieves are: 
* Power  = 1.632mW
* Gain   = 80.00 dB
* BandWidth = 50.4MHz
* Spot Noise @ 1kHz =    
* FOM (Gain x Bandwidth) / (Power dissipation)= 2650
***************************************************************

** Including the model file
.include /afs/ir.stanford.edu/class/ee114/hspice/ee114_hspice.sp

* Defining Top level circuit parameters
.param Cin = 100f
.param CL  = 250f
.param RL  = 20k

* defining the supply voltages

vdd vdd gnd 2.5
vss vss gnd -2.5

* defining the bias voltages
vpbias pbias gnd 1.8
vnbias nbias gnd -1.3
 
* Defining the input current source

* Note, having each source with ac magnitude of 0.5 (as below) ensures a differential input magnitude of 1
** For ac simulation uncomment the following 2 lines** 
Iina		iina	vdd	ac	0.5	
Iinb		vdd	iinb	ac	0.5	

** For transient simulation uncomment the following 2 lines**
**Iina		iina	vdd	sin(0 0.5u 1e6)
**Iinb		vdd	iinb	sin(0 0.5u 1e6)

* Defining Input capacitance

Cina	vdd	iina 'Cin'
Cinb	vdd	iinb 'Cin'

* Defining the differential load 

RL	vouta		voutb		'RL'
CL	vouta		voutb		'CL'

*** Your Trans-impedance Amplifier here ***
***	d	g	s	b	n/pmos114	w	l

*** A Side ***
m1a    d1a      gnd      iina      vss    nmos114 w=10u l=1u
m2a    d2a      d1a      s2a       vss    nmos114 w=5u l=1u
m3a    vdd      d2a      vouta    vss     nmos114 w=28u l=1u 

rua      vdd      d1a      32.8k 
rda      d1a      vss      900k

*** B Side ***
m1b     d1b     gnd      iinb    vss      nmos114 w=10u l=1u
m2b     d2b     d1b      s2a     vss      nmos114 w=5u l=1u 
m3b     vdd     d2b      voutb   vss      nmos114 w=28u l=1u 

rub       vdd      d1b      32.8k 
rdb       d1b      vss      900k

*** Current Bias ***
***pmos***
ml1a     d1a      pbias    vdd      vdd    pmos114 w=2u l=2u 
ml1b     d1b      pbias    vdd      vdd    pmos114 w=2u l=2u 

***nmos***  
ml2a     vdd      vdd      d2a      vss      nmos114 w=2.2u l=2u 
ml2b     vdd      vdd      d2b      vss      nmos114 w=2.2u l=2u  
*** Your Bias Circuitry here ***

mbias1a      iina    nbias      vss     vss      nmos114 w=7u l=2u    
mbias1b      iinb    nbias      vss     vss      nmos114 w=7u l=2u
mbias2a      s2a     nbias      vss     vss      nmos114 w=2u l=2.4u
mbias2b      s2a     nbias      vss     vss      nmos114 w=2u l=2.4u
mbias3a      vouta   nbias      vss     vss      nmos114 w=8u l=2u
mbias3b      voutb   nbias      vss     vss      nmos114 w=8u l=2u

* defining the analysis

.op
.option post brief nomod

** For ac simulation uncomment the following line** 
.ac dec 1k 100 1g
* add line for noise simulation

.measure ac gainmaxa max vdb(vouta)
.measure ac f3dba when vdb(vouta)='gainmaxa-3'

.measure ac gainmaxb max vdb(voutb)
.measure ac f3dbb when vdb(voutb)='gainmaxb-3'

* Note, the statement below gives you the differential gain
.measure ac gaindiff max vdb(vouta, voutb)

** For transient simulation uncomment the following line **
**.tran 0.01u 4u 

.end